DEFINT A-Z

TYPE BMPHeaderT
    id AS STRING * 2
    size AS LONG
    rr1 AS INTEGER
    rr2 AS INTEGER
    offset AS LONG
    hsize AS LONG
    wid AS LONG
    hei AS LONG
    planes AS INTEGER
    bpp AS INTEGER
    pakbyte AS LONG
    imagebytes AS LONG
    xres AS LONG
    yres AS LONG
    colch AS LONG
    ic AS LONG
END TYPE

DIM SHARED ASM(9) AS INTEGER,DPAL(15,2) AS INTEGER
DIM BMPFILE(11) AS STRING
DIM BF(2000) AS INTEGER

FOR I=0 TO 11:READ BMPFILE(I):NEXT I
DATA "00.BMP","01.BMP","02.BMP","03.BMP"
DATA "04.BMP","05.BMP","06.BMP","07.BMP"
DATA "08.BMP","09.BMP","10.BMP","11.BMP"

FOR I=0 TO 15:READ DPAL(I,0),DPAL(I,1),DPAL(I,2):NEXT I
DATA &H00,&H00,&H00, &H00,&H00,&Ha8, &H00,&Ha8,&H00, &H00,&Ha8,&Ha8
DATA &Ha8,&H00,&H00, &Ha8,&H00,&Ha8, &Ha8,&Ha8,&H00, &Ha8,&Ha8,&Ha8
DATA &H54,&H54,&H54, &H54,&H54,&Hff, &H54,&Hff,&H54, &H54,&Hff,&Hff
DATA &Hff,&H54,&H54, &Hff,&H54,&Hff, &Hff,&Hff,&H54, &Hff,&Hff,&Hff

DEF SEG=VARSEG(ASM(0)):FOR I=0 TO 14:READ N:POKE VARPTR(ASM(0))+I,N:NEXT I:DEF SEG
DATA &H55,&H89,&He5,&Hb8,&H03,&H10,&H8a,&H5e,&H06,&Hcd,&H10,&H5d,&Hca,&H02,&H00

CALL LOADBMP("FRAMES\"+BMPFILE(0),BF())

DEF SEG=VARSEG(ASM(0)):CALL ABSOLUTE(BYVAL 0,VARPTR(ASM(0))):DEF SEG
CALL DRAWSCR(BF())

WHILE INKEY$="":WEND
DEF SEG=VARSEG(ASM(0)):CALL ABSOLUTE(BYVAL 1,VARPTR(ASM(0))):DEF SEG
CLS:END

SUB LOADBMP(FBMP AS STRING,BUF() AS INTEGER)
  STATIC hdr AS BMPHeaderT,PAL AS STRING*64,LN AS STRING*80
  STATIC I AS INTEGER,Y AS INTEGER,P AS INTEGER
  STATIC A AS INTEGER,B AS INTEGER,A1,B1
  DIM COLTAB(15) AS INTEGER

  OPEN FBMP FOR BINARY AS #1
  GET #1,1,hdr
  IF "BM"<>hdr.id THEN PRINT "Invalid BMP File.":END
  W=hdr.wid: H=hdr.hei
  IF W<>80 OR H<>50 OR hdr.bpp<>4 THEN PRINT "Not 80x50x16 BMP file.":END
  rowBytesBmp=((W+31)\32)*4
  GET #1,15+hdr.hsize,PAL
  FOR I=0 TO 15
    B=ASC(MID$(PAL,I*4+1,1))
    G=ASC(MID$(PAL,I*4+2,1))
    R=ASC(MID$(PAL,I*4+3,1))
    COLTAB(I)=GETCOLOR(R,G,B)
  NEXT I
  P=0
  FOR Y=0 TO 24
    GET #1,15+hdr.hsize+64+(24-Y)*80,LN
    FOR I=1 TO 40
      A=ASC(MID$(LN,I+40,1)):B=ASC(MID$(LN,I,1))
      A1=COLTAB(A\16):B1=COLTAB(B\16)
      BUF(P)=(A1 *16) OR B1:P=P+1
      A1=COLTAB(A AND 15):B1=COLTAB(B AND 15)
      BUF(P)=(A1 *16) OR B1:P=P+1
    NEXT I
  NEXT Y

  CLOSE #1
END SUB

FUNCTION GETCOLOR(R AS INTEGER,G AS INTEGER,B AS INTEGER)
  STATIC I AS INTEGER,D AS LONG,DMIN AS LONG,RS AS INTEGER
  DMIN=-1:RS=0
  FOR I=0 TO 15
    D=(R-DPAL(I,0))^2 + (G-DPAL(I,1))^2 + (B-DPAL(I,2))^2
    IF DMIN=-1 OR D<DMIN THEN DMIN=D:RS=I
  NEXT I
  GETCOLOR=RS
END FUNCTION

SUB DRAWSCR(A() AS INTEGER)
  STATIC I AS INTEGER
  DEF SEG=&HB800
  FOR I=0 TO 1999:POKE I*2,220:POKE I*2+1,A(I):NEXT I
  DEF SEG
END SUB

